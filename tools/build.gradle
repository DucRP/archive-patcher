plugins {
	id "java-library"
	id "maven-publish"
}

configurations {
	internal
	runtimeClasspath.extendsFrom(internal)
	compileClasspath.extendsFrom(internal)
	testImplementation.extendsFrom(internal)
}

dependencies {
	internal project(':applier')
	internal project(':explainer')
	internal project(':generator')
	internal project(':shared')
}

jar {
	manifest {
		attributes "Main-Class": "com.google.archivepatcher.tools.FileByFileTool"
	}

	duplicatesStrategy = "EXCLUDE"
	archiveBaseName.set("archive-patcher-all")

	dependsOn configurations.internal
	from configurations.internal.collect { it.isDirectory() ? it : zipTree(it) }

	from "../LICENSE"
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java
			artifactId "archive-patcher-all"
		}
	}
}